MODULE main
VAR
	room : {idleNoHeat, waitForHeat, idleHeat, waitForCool};
	controller : {idle, actHeater, heaterRun, off, error };
	furnace : {furnaceRun, furnaceOff, furnaceAct, furnaceErr };
	
	roomTemp : 68..74;
	waitedForWarm : 0..3;
	waitedForCool : 0..3;
	valvePos : 0..2;
	furnaceStartupTime : 0..3;
	
	requestHeat :  boolean;
	furnaceRunning : boolean;
	activate : boolean;
	deactivate : boolean;
	
	furnaceFault : boolean;
	furnaceReset : boolean;
	userReset : boolean;
	heatingSwitchOff : boolean;
	heatingSwitchOn : boolean;
	
DEFINE
	desiredTemp := 71;
	warmUpTimer := 3;
	coolDownTimer := 3;

	furnaceTimer := 3;

	tooCold := (desiredTemp - roomTemp)> 2;
	tooHot := (roomTemp - desiredTemp)> 2;

	noHeatReq := (room = idleNoHeat) | (room = waitForHeat);
	heatReq := (room = idleHeat) | (room = waitForCool);

	heaterActive := (controller = actHeater) | (controller = heaterRun);
	furnaceNormal := (furnace = furnaceRun) | (furnace = furnaceOff) | (furnace = furnaceAct);

ASSIGN
--requestHeat var
init(requestHeat) := TRUE;

--controller states
init(controller) := idle;
init(userReset) := TRUE;
init(heatingSwitchOff) := FALSE;
init(heatingSwitchOn) := TRUE;

next(controller) :=
	case
		(controller = idle) & (requestHeat = TRUE) : actHeater;
		(controller = actHeater) & (furnaceRunning = TRUE) : heaterRun;
		(controller = actHeater) & (requestHeat = FALSE) : idle;
		(controller = heaterRun) & (requestHeat = FALSE) : idle;
		
		(controller = idle) & (furnaceFault) : error;
		(controller = actHeater) & (furnaceFault) : error;
		(controller = heaterRun) & (furnaceFault) : error;

		(controller = idle) & (heatingSwitchOff) : off;
		(controller = actHeater) & (heatingSwitchOff) : off;
		(controller = heaterRun) & (heatingSwitchOff) : off;
		
		(controller = off) & (heatingSwitchOn) : idle;
		(controller = error) & (userReset) : off;
		
		
		TRUE : controller;
	esac;
		
--activate
init(activate) := FALSE;
next(activate) :=
	case
		(controller = idle) & (requestHeat = TRUE) : TRUE;
		TRUE : activate;
	esac;
		

--deactivate
init(deactivate) := FALSE;
next(deactivate) :=
	case
		(controller = actHeater) & (requestHeat = FALSE) : TRUE;
		(controller = heaterRun) & (requestHeat = FALSE) :TRUE;

		(controller = idle) & (heatingSwitchOff) : TRUE;
		(controller = actHeater) & (heatingSwitchOff) : TRUE;
		(controller = heaterRun) & (heatingSwitchOff) : TRUE;
		
		TRUE : activate;
	esac;
	
-- furnace
init(furnace) := furnaceOff;
init(furnaceFault) := FALSE;

next(furnace) :=
	case
		(furnace = furnaceOff) & (activate = TRUE) : furnaceAct;
		(furnace = furnaceAct) & (deactivate = TRUE) : furnaceOff;
		(furnace = furnaceAct) & (furnaceStartupTime< furnaceTimer): furnaceAct;
		(furnace = furnaceAct) & (furnaceStartupTime = furnaceTimer): furnaceRun;
		(furnace = furnaceRun) & (deactivate = TRUE) : furnaceOff;
		
		(furnace = furnaceOff) & (furnaceFault = TRUE) : furnaceErr;
		(furnace = furnaceErr) & (furnaceReset = TRUE) : furnaceOff;
		TRUE : furnace;
	esac;

-- furnaceStartupTime
init(furnaceStartupTime) := 0;
next(furnaceStartupTime) :=
	case
		(furnace = furnaceOff)& (activate = TRUE) : 0;
		(furnace = furnaceAct) & (furnaceStartupTime< furnaceTimer): furnaceStartupTime + 1 ;
		(furnace = furnaceErr) & (furnaceReset = TRUE) : 0;
		TRUE : furnaceStartupTime;
	esac;
-- furnaceRunning
init(furnaceRunning) := FALSE;
next(furnaceRunning) :=
	case
		(furnace = furnaceAct) & (furnaceStartupTime = furnaceTimer): TRUE ;
		TRUE : furnaceRunning;
	esac;
	
-- furnaceReset
init(furnaceReset) := FALSE;
next(furnaceReset) :=
	case
		(controller = error) & (userReset = TRUE): TRUE ;
		TRUE : furnaceReset;
	esac;
	
LTLSPEC G ( (furnace = furnaceRun) & ( furnaceFault = TRUE)  -> F( (furnace = furnaceErr)))
LTLSPEC ( ((furnace = furnaceRun) & ( furnaceFault) & X(!heatingSwitchOff))   -> X(controller = error))
--Property 4
LTLSPEC G ((furnace = furnaceRun ) | (furnace = furnaceAct)) -> ((controller = actHeater) | (controller = heaterRun))